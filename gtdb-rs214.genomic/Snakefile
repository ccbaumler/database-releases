### update the wort manifest to reflect all available signatures; use updated version here
DATABASES = ['/group/ctbrowngrp/sourmash-db/wort-manifests/2023-05-05.wort.sqlmf'] # wort manifest

configfile: "config.yml"

NAME = config['name']
TAG = config['tag']
KSIZES = config['ksizes']
SCALED = config['scaled']

# taxonomy info
TAXONOMY_FILE = f"{NAME}-{TAG}.lineages.csv"
REPS_TAXONOMY_FILE = f"{NAME}-{TAG}.lineages.reps.csv"

wildcard_constraints:
    filename = "[^/]+"
ARCHAEA_URL=config['archaea_metadata_url']
ARCHAEA_FILE=config['archaea_metadata_filename']
BACTERIA_URL=config['bacteria_metadata_url']
BACTERIA_FILE=config['bacteria_metadata_filename']
########################################

wildcard_constraints:
    ksize = "\d+",
    name = "[^/]+",
    tag = "[^/\.]+",
    filename = "[^/]+"

rule prepare:
    input:
        expand("gtdb-{tag}.missing.csv", tag=TAG),
        expand("gtdb-{tag}.manifest.csv", tag=TAG),

rule build:
    input:
        expand("gtdb-{tag}-k{k}.abund.zip", tag=TAG, k=KSIZES),
        expand("gtdb-{tag}-reps.k{k}.abund.zip", tag=TAG, k=KSIZES),

rule check:
    input:
        expand("{name}-{tag}-k{k}.abund.zip.check", name=NAME, tag=TAG, k=KSIZES),
        expand("{name}-{tag}-reps.k{k}.abund.zip.check", name=NAME, tag=TAG, k=KSIZES),

rule download_gtdb_metadata:
    output:
        arch_metadata='ar53_metadata_r214.tsv',
        bac_metadata='bac120_metadata_r214.tsv',
    params:
        arch_url=ARCHAEA_URL,
        bact_url=BACTERIA_URL,
        arch_file=ARCHAEA_FILE,
        bact_file=BACTERIA_FILE,
    shell: 
        """
        wget {params.arch_url}
        wget {params.bact_url}
        tar xzvf {params.arch_file}
        tar xzvf {params.bact_file}
        rm -rf *tar.gz
        """

rule make_taxonomy:
    input:
        ar53_metadata='ar53_metadata_r214.tsv',
        bac120_metadata='bac120_metadata_r214.tsv',
    output:
        tax_csv=TAXONOMY_FILE,
        reps_csv=REPS_TAXONOMY_FILE,
    shell:
        """
        python make-gtdb-taxonomy.py --metadata-files {input.ar53_metadata} {input.bac120_metadata} \
               -o {output.tax_csv} --reps-csv {output.reps_csv}
        """

rule picklist_check:
    input:
        databases = DATABASES,
        picklist = TAXONOMY_FILE,
    output:
        missing = "gtdb-{tag}.missing.csv",
        manifest = "gtdb-{tag}.manifest.csv",
    shell:
        """
        sourmash sig check --picklist {input.picklist}:ident:ident \
            {input.databases} --output-missing {output.missing} \
            --save-manifest {output.manifest}
        touch {output.missing}
        """

rule build_abund_zip:
    input:
        databases = DATABASES,
        manifest = "gtdb-{tag}.manifest.csv",
    output:
        "gtdb-{tag}-k{k}.abund.zip"
    shell:
        """
        sourmash sig cat {input.manifest} -k {wildcards.k} -o {output}
        """

rule build_representatives_zip:
    input:
        abund_zip = "gtdb-{tag}-k{k}.abund.zip",
        reps_picklist = REPS_TAXONOMY_FILE ,
    output:
        "gtdb-{tag}-reps.k{k}.abund.zip"
    shell:
        """
        sourmash sig extract {input.abund_zip} --picklist {input.reps_picklist}:ident:ident -k {wildcards.k} -o {output}
        """

rule picklist_confirm:
    input:
        picklist = TAXONOMY_FILE, 
        zip = "gtdb-{tag}-k{k}.abund.zip",
    output:
        confirm = touch("gtdb-{tag}-k{k}.abund.zip.check")
    shell:
        """
        sourmash sig check --picklist {input.picklist}:ident:ident \
            {input.zip} --fail
        """

rule picklist_confirm_reps:
    input:
        picklist = REPS_TAXONOMY_FILE, 
        zip = "gtdb-{tag}-reps.k{k}.abund.zip",
    output:
        confirm = touch("gtdb-{tag}-reps.k{k}.abund.zip.check")
    shell:
        """
        sourmash sig check --picklist {input.picklist}:ident:ident \
            {input.zip} --fail
        """

